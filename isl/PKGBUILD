# Maintainer: Jashandeep Sohi <jashandeep.s.sohi@gmail.com>

pkgname='isl'
pkgver='0.14'
pkgrel=1
pkgdesc='Library for manipulating sets and relations of integer points bounded by linear constraints.'
url='http://isl.gforge.inria.fr/'
license=('MIT')
source=( "http://isl.gforge.inria.fr/$pkgname-$pkgver.tar.bz2")
md5sums=('acd347243fca5609e3df37dba47fd0bb')
arch=('i686' 'x86_64')
makedepends=('gmp')

build() { 
 cd "$srcdir/$pkgname-$pkgver"
 
 CPPFLAGS="$CPPFLAGS -O2"
 
 ./configure \
  --prefix="/usr"
 make
}

check() {
 cd "$srcdir/$pkgname-$pkgver"
 make check
}

package() {
 cd "$srcdir/$pkgname-$pkgver"
 make DESTDIR="$pkgdir" install
 
 install -dm755 "$pkgdir/usr/share/gdb/auto-load/usr/lib"
 mv "$pkgdir/usr/lib/"*-gdb.py "$pkgdir/usr/share/gdb/auto-load/usr/lib/."
 
 install -Dm644 -t "$pkgdir/usr/share/licenses/isl" LICENSE
}

# vim: tabstop=1 expandtab
